# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname='linux-user-chroot-git'
pkgver=0.0.0
pkgrel='1'
pkgdesc='setuid helper for making bind mounts and chrooting'
url='http://git.gnome.org/browse/linux-user-chroot/tree/README'
arch=('i686' 'x86_64')
license='GPL'
depends=('glibc')
makedepends=('git' 'libxslt')
provides=('linux-user-chroot')
conflicts=('linux-user-chroot')

_gitroot="git://git.gnome.org/linux-user-chroot.git"
_gitname=linux-user-chroot
source=(${_gitname}::${_gitroot})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build () {
	cd ${srcdir}/${_gitname}
	./autogen.sh --prefix=/usr --enable-newnet-helper
	make
}

package() {
	cd ${srcdir}/${_gitname}
	make DESTDIR="${pkgdir}" install
	chmod +s "${pkgdir}/usr/bin"/linux-user-chroot{,-newnet}
}
