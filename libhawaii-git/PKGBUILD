# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=libhawaii-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii libraries"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('LGPL2.1')
depends=('qtbase-git' 'qtdeclarative-git')
makedepends=('git' 'gdb' 'cmake' 'extra-cmake-modules-git' 'qtchooser')
options=('debug')

_gitroot="git://github.com/mauios/libhawaii.git"
_gitbranch=dev
_gitname=libhawaii
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export PATH=/opt/qt-git/bin:$PATH
	export PKG_CONFIG_PATH=/opt/hawaii-git/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/hawaii-git/lib:/opt/qt-git/lib
	export QT_SELECT=git

	cd build
	cmake ../${_gitname} \
		-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=/opt/hawaii-git/lib \
		-DADDITIONAL_LDLIBPATH=/opt/qt-git/lib \
		-DADDITIONAL_PATH=/opt/qt-git/bin \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
