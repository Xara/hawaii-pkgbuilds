# Maintainer: LEW21 <lew21@xtreeme.org>

pkgname=mesa-full
pkgver=20130215
_realver=9.1
pkgrel=1
pkgdesc="Full Mesa 3D graphics library with all its components, built from the git master branch. Built with Wayland support."
arch=(i686 x86_64)
url="http://mesa3d.org/"
license=('LGPL')
depends=('glibc' 'libdrm>=2.4.39' 'libxxf86vm>=1.1.2' 'libxdamage>=1.1.3' 'expat>=2.1.0' 'gcc-libs' 'systemd' 'libxcb' 'libxext' 'libxfixes' 'wayland')
makedepends=('git' 'glproto>=1.4.16' 'libx11>=1.5.0' 'libxt>=1.1.3' 'gcc-libs>=4.7.1-6' 'dri2proto>=2.8' 'python2' 'libxml2' 'imake' 'llvm-amdgpu-git' 'libvdpau>=0.5')
optdepends=('libtxc_dxtn: S3TC support'
'mesa-demos: glxinfo and glxgears'
'opengl-man-pages: for the OpenGL API man pages')
provides=("mesa-full-wayland=${pkgver}" "mesa=${_realver}" "libgl=${_realver}" "libglapi=${_realver}" "libgles=${_realver}" "libegl=${_realver}" "ati-dri=${_realver}" "intel-dri=${_realver}" "nouveau-dri=${_realver}", "khrplatform-devel=${_realver}" "libgbm=${_realver}")
conflicts=('mesa' 'libgl' 'libglapi' 'libgles' 'libegl' 'ati-dri' 'intel-dri' 'nouveau-dri' 'mach64-dri' 'mga-dri' 'r128-dri' 'savage-dri' 'tdfx-dri' 'unichrome-dri', 'khrplatform-devel' 'libgbm')

_gitroot="git://anongit.freedesktop.org/git/mesa/mesa"
_gitname="mesa"

build() {
	msg "Connecting to the GIT server...."

	if [ -d $_gitname ] ; then
		cd $_gitname
		git pull origin master
		cd ..
	else
		git clone $_gitroot --depth=1
	fi

	msg "Creating build directory..."
	rm -rf $_gitname-build
	cp -rH $_gitname $_gitname-build

	msg "Starting build..."
	cd $_gitname-build

	export PYTHON=/usr/bin/python2

	# Classic r300, r600 & swrast are disabled - their Gallium versions are better.
	# Classic nouveau is for different hardware than Gallium nouveau, so both are enabled.

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--with-dri-driverdir=/usr/lib/xorg/modules/dri \
		--with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
		--with-dri-drivers=i915,i965,r200,radeon,nouveau \
		--enable-gallium-llvm \
		--enable-egl \
		--enable-gallium-egl \
		--with-egl-platforms=drm,x11,wayland \
		--enable-gallium-gbm \
		--enable-gles1 \
		--enable-gles2 \
		--enable-openvg \
		--enable-glx-tls \
		--enable-texture-float \

	make
}

package() {
	cd ${srcdir}/$_gitname-build

	make DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
	ln -sf libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so
}
