# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-vibe-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii: Vibe libraries"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('LGPL')
depends=('kf5-kdelibs-git' 'qttools-git' 'cantarell-fonts')
makedepends=('git' 'cmake' 'qtdeclarative-git')

_gitroot="git://github.com/hawaii-desktop/vibe.git"
_gitbranch=master
_gitname=vibe
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}

	export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
	export QT_SELECT=git

	mkdir build
	cd build
	cmake .. \
		-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd ${srcdir}/${_gitname}/build
	make DESTDIR="${pkgdir}" install
}
