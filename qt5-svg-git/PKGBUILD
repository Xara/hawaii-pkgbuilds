# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-svg-git
pkgver=0.0.0
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtSvg)"
groups=('qt' 'qt5')
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
makedepends=('git')
depends=('qt5-base-git')
replaces=('qt5-qtsvg-git')
provides=('qt5-svg')
conflicts=('qt5-svg')
options=('!libtool')
_gitroot="git://gitorious.org/qt/qtsvg.git"
_gitname="qt5-svg"
_gitbranch=stable
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}
	qmake
	make
}

package() {
	cd ${srcdir}/${_gitname}
	make INSTALL_ROOT="${pkgdir}" install
}
