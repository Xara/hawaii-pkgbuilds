# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qtmultimedia-git
pkgver=v5.1.0.beta1.3.g30fd950
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtMultimedia)"
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qtdeclarative-git' 'gstreamer0.10' 'gstreamer0.10-base' 'pulseaudio' 'libpulse')
makedepends=('git')
options=('!libtool' 'debug')
source=(git://gitorious.org/qt/qtmultimedia.git#branch=dev
        build-fix.patch)
md5sums=('SKIP'
         'fca12ca4e9c315bd0cd4928fe4be70eb')

pkgver() {
  cd qtmultimedia
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd qtmultimedia
  patch -p1 -i "${srcdir}"/build-fix.patch
}

build() {
  cd qtmultimedia
  /opt/qt-git/bin/qmake
  make
}

package() {
  cd qtmultimedia
  make INSTALL_ROOT="${pkgdir}" install
}
