# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-terminal-git
pkgver=0.0.0
pkgrel=1
pkgdesc="The Hawaii Terminal Emulator"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL2')
depends=('qt5-declarative' 'qt5-quickcontrols' 'yat-git')
makedepends=('git' 'cmake' 'qtchooser')
options=('debug')

_gitroot="git://github.com/mauios/hawaii-terminal.git"
_gitbranch=dev
_gitname=terminal
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export QT_SELECT=5

	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
