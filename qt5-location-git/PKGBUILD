# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-location-git
pkgver=0.0.0
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtLocation)"
groups=('qt' 'qt5')
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-base-git' 'qt5-jsbackend-git' 'qt5-declarative-git' 'qt5-3d-git')
makedepends=('git')
replaces=('qt5-qtlocation-git')
provides=('qt5-location')
conflicts=('qt5-location')
options=('!libtool')
_gitroot="git://gitorious.org/qt/qtlocation.git"
_gitname="qt5-location"
_gitbranch=master
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}
	qmake
	make
}

package() {
	cd ${srcdir}/${_gitname}
	make INSTALL_ROOT="${pkgdir}" install
}
