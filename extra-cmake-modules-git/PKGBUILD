# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

# Based on extra-cmake-modules-git by Martin Sandsmark <sandsmark@iskrembilen.com>

pkgname=extra-cmake-modules-git
pkgver=0.0.0
pkgrel=1
pkgdesc='Extra CMake modules'
arch=('i686' 'x86_64')
url='http://quickgit.kde.org/index.php?p=extra-cmake-modules.git&a=summary'
license=('LGPL')
depends=('cmake')
makedepends=('pkgconfig' 'cmake' 'git')
options=(!strip)
_gitroot='git://anongit.kde.org/extra-cmake-modules.git'
_gitname='extra-cmake-modules'
#_gitbranch='a667c2d45d041f2dc365a7ca82a27355aed0b1cc'
_gitbranch='master'
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}

	rm -rf build
	mkdir build
	cd build

	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make || return 1
}

package() {
	cd ${srcdir}/${_gitname}/build
	make DESTDIR=${pkgdir} install || return 1
}
