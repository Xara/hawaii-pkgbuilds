# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-connectivity-git
pkgver=0.0.0
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtConnectivity)"
groups=('qt' 'qt5')
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-base-git' 'qt5-declarative-git' 'bluez')
makedepends=('git')
replaces=('qt5-qtconnectivity-git')
conflicts=('qt5-connectivity')
provides=('qt5-connectivity')
options=('!libtool')
_gitroot="git://gitorious.org/qt/qtconnectivity.git"
_gitname="qt5-connectivity"
_gitbranch=master
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}
	qmake
	make
}

package() {
	cd ${srcdir}/${_gitname}
	make INSTALL_ROOT="${pkgdir}" install
}
