# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-qtwebkit-git
pkgver=20130322
pkgrel=1
pkgdesc="Qt 5: qtwebkit module"
groups=('qt5' 'qt5-addons')
arch=('i686' 'x86_64')
url="https://gitorious.org/qtwebkit/qt5-module"
license=('LGPL')
depends=('sqlite' 'libwebp' 'gstreamer0.10' 'gstreamer0.10-base' 'qt5-qtdeclarative-git' 'qt5-qtlocation-git' 'qt5-qtsensors-git')
makedepends=('git' 'perl' 'python2' 'gperf' 'flex' 'bison' 'fontconfig' 'libxml2' 'libxslt' 'ruby')
options=('!libtool')
source=()
md5sums=()

#_gitroot="git://gitorious.org/qtwebkit/qt5-module.git"
_gitroot="git://gitorious.org/qt/qtwebkit.git"
_gitname=qt5-qtwebkit
_gitbranch=dev

build() {
	if [ ! -d "${_gitname}" ]; then
		git clone -b ${_gitbranch} --depth 1 ${_gitroot} ${_gitname} && cd ${_gitname}
	else
		cd ${_gitname} && git reset --hard && git pull origin && git clean -dfx
	fi

	msg "GIT checkout done."

	sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
		-e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
		$(find . -name '*.py')
	sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
		-e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
		-e "s|python |python2 |" \
		$(find . -name '*.pr?') $(find . -name '*.make')
	sed -i -e "s|setWindowFlags|setFlags|" $(find Tools/MiniBrowser -name '*.cpp')

	export PYTHON=python2
	#qmake CONFIG+=use_system_icu
	Tools/Scripts/build-webkit --qt --qmake=qmake --qmakearg="CONFIG+=use_system_icu" --release
}

package() {
	cd "${_gitname}"
	make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install
	#make INSTALL_ROOT="${pkgdir}" install
}
