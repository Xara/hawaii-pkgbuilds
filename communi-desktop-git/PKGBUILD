# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=communi-desktop-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Communi for desktop"
arch=('i686' 'x86_64')
url="https://github.com/communi/communi-desktop"
license=('GPL2')
conflicts=('communi-desktop')
replaces=('communi-desktop')
depends=('libcommuni-git')
makedepends=('gcc' 'git' 'qtchooser')

_gitroot="git://github.com/communi/communi-desktop.git"
_gitname=communi-desktop
_gitbranch=master
source=(${_gitname}::${_gitroot}#branch=${_gitbranch}
        git://github.com/communi/communi-shared.git)
md5sums=('SKIP' 'SKIP')

pkgver() {
	cd ${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
	cd ${_gitname}
	git submodule init
	git config submodule.src/libs/base/shared.url $srcdir/communi-shared
	git submodule update
}

build() {
	export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
	export QT_SELECT=git

	cd ${_gitname}

	qmake
	make
}

package() {
	cd ${_gitname}
	make INSTALL_ROOT="${pkgdir}" install

	# Since we are using Qt from git we create a script that
	# sets LD_LIBRARY_PATH and runs the executable
	path=/usr
	exe=communi
	mv $pkgdir/$path/bin/$exe $pkgdir/$path/bin/$exe.real
	cat > $pkgdir/$path/bin/$exe <<EOF
#!/bin/bash
export LD_LIBRARY_PATH=/opt/qt-git/lib
exec $path/bin/$exe.real "\$@"
EOF
	chmod 755 $pkgdir/$path/bin/$exe
}
