# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=greenisland-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Green Island: Qt-based compositor infrastructure for Wayland"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL', 'LGPL')
depends=('qt5-declarative' 'hawaii-qt5-wayland-git')
makedepends=('git' 'cmake' 'qtchooser')
options=('debug')

_gitroot="git://github.com/mauios/greenisland.git"
_gitbranch=dev
_gitname=greenisland
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir build
}

build() {
	export QT_SELECT=5

	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=/opt/hawaii-git/lib \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
