# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-xmlpatterns-git
pkgver=0.0.0
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtXmlPattenrs)"
groups=('qt' 'qt5')
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-base-git')
makedepends=('git')
replaces=('qt5-qtxmlpatterns-git')
provides=('qt5-xmlpatterns')
conflicts=('qt5-xmlpatterns')
options=('!libtool')
_gitroot="git://gitorious.org/qt/qtxmlpatterns.git"
_gitname="qt5-xmlpatterns"
_gitbranch=stable
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}

	qmake
	make

	# It seems the tools subdir doesn't get built
	cd tools
	qmake
	make
}

package() {
	cd ${srcdir}/${_gitname}

	make INSTALL_ROOT="${pkgdir}" install

	cd tools
	make INSTALL_ROOT="${pkgdir}" install
}
