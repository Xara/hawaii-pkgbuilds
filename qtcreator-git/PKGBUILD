# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>
# Contributor: delor <bartekpiech@gmail com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: heinz from #qt-creator

pkgname=qtcreator-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Lightweight, cross-platform integrated development environment"
arch=('i686' 'x86_64')
url="http://qt-project.org"
license=('LGPL')
depends=('qtscript-git' 'qtquick1-git' 'qttools-git' 'qtxmlpatterns-git')
makedepends=('git' 'gdb')
optdepends=('gdb: for the debugger')
replaces=('qtcreator-for-qt5-git')
provides=('qtcreator')
conflicts=('qtcreator')
_gitroot="git://gitorious.org/qt-creator/qt-creator.git"
_gitname="qt-creator"
_gitbranch="master"
source=(${_gitname}::${_gitroot}#branch=${_gitbranch}
        'qtcreator.desktop')
md5sums=('SKIP'
         '164356c75a347f5755ce743ceac0d334')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    if [ -d ${srcdir}/build ]; then
        cd ${srcdir}/build
    else
        mkdir ${srcdir}/build
        cd ${srcdir}/build
    fi
    mkdir -p share/doc/qtcreator
    touch share/doc/qtcreator/qtcreator.qch

    # to enable the qml designer you need to add QT_PRIVATE_HEADERS=/path/to/your/qt/source/include to the qmake command
    qmake ${srcdir}/${_gitname}/qtcreator.pro
    make
}

package() {
    cd "${srcdir}/build"

    make INSTALL_ROOT="${pkgdir}/usr" install
    install -Dm644 ${srcdir}/qtcreator.desktop ${pkgdir}/usr/share/applications/qtcreator.desktop || return 1
    install -Dm644 ${srcdir}/$_gitname/LGPL_EXCEPTION.TXT ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT || return 1
}
