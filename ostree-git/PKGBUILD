# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname='ostree-git'
pkgver=0.0.0
pkgrel=1
url="http://git.gnome.org/browse/ostree/tree/README.md"
pkgdesc='The GNOME OS build tool'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
provides=('ostree')
makedepends=('git' 'python2' 'pkg-config' 'gnome-common' 'intltool' 'gtk-doc' 'automake')
depends=('libarchive' 'linux-user-chroot-git')

_gitroot="git://git.gnome.org/ostree"
_gitname="ostree"
source=(${_gitname}::${_gitroot})
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_gitname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${srcdir}/${_gitname}

  git submodule update --init

  msg2 "Downloading embedded dependencies..."
  make -C embedded-dependencies -f Makefile.bootstrap embedded-deps-download
  sed -i 's|/usr/bin/env python|/usr/bin/python2|' embedded-dependencies/libsoup/libsoup/tld-parser.py

  msg2 "Configuring..."
  export PYTHON=/usr/bin/python2
  ./autogen.sh --prefix=/usr \
    --disable-documentation \
    --with-soup-gnome
  msg2 "Compiling..."
  make
}

package() {
  cd "$srcdir/$_gitname"

  msg2 "Packaging..."
  make install DESTDIR="$pkgdir"

  mkdir "${pkgdir}/ostree"
}
