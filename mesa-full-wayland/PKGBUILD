# Maintainer: LEW21 <lew21@xtreeme.org>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=mesa-full-wayland
pkgver=20121227
_realver=9.1
pkgrel=1
pkgdesc="Full Mesa 3D graphics library with all its components, built from the git master branch. Identical to mesa-full, except it adds Wayland support."
arch=(i686 x86_64)
url="http://mesa3d.org/"
license=('LGPL')
depends=('libdrm-git' 'dri2proto>=2.3' 'glproto>=1.4.12' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1' 'llvm' 'wayland-git')
makedepends=('git' 'pkgconfig' 'imake' 'python2' 'python2-lxml')
optdepends=('libtxc_dxtn: S3TC support'
'mesa-demos: glxinfo and glxgears')
provides=("mesa=${_realver}" "libgl=${_realver}" "libglapi=${_realver}" "libgles=${_realver}" "libegl=${_realver}" "ati-dri=${_realver}" "intel-dri=${_realver}" "nouveau-dri=${_realver}", "khrplatform-devel=${_realver}" "libgbm=${_realver}")
conflicts=('mesa' 'libgl' 'libglapi' 'libgles' 'libegl' 'ati-dri' 'intel-dri' 'nouveau-dri' 'mach64-dri' 'mga-dri' 'r128-dri' 'savage-dri' 'tdfx-dri' 'unichrome-dri', 'khrplatform-devel' 'libgbm')
source=('fix-shm-roundtrip.patch')
md5sums=('77af34e011e612b26d8808feb0ac4833')

_gitroot="git://anongit.freedesktop.org/git/mesa/mesa"
_gitname="mesa"

build() {
	msg "Connecting to the GIT server...."

	if [ -d $_gitname ] ; then
		cd $_gitname
		git pull origin master
		cd ..
	else
		git clone $_gitroot --depth=1
	fi

	msg "Creating build directory..."
	rm -rf $_gitname-build
	cp -rH $_gitname $_gitname-build

	msg "Starting build..."
	cd $_gitname-build

	patch -Np1 -i ${srcdir}/fix-shm-roundtrip.patch

	# Classic r300, r600 & swrast are disabled - their Gallium versions are better.
	# Classic nouveau is for different hardware than Gallium nouveau, so both are enabled.

	# --enable-texture-float is not passed to configure, see
	# http://cgit.freedesktop.org/mesa/mesa/tree/docs/patents.txt

	./autogen.sh --prefix=/usr --with-dri-driverdir=/usr/lib/xorg/modules/dri \
		--with-dri-drivers=i915,i965,r200,radeon,swrast \
		--with-gallium-drivers=r300,r600,nouveau,svga,swrast \
		--with-egl-platforms=drm,x11,fbdev,wayland \
		--with-state-trackers=egl \
		--enable-gallium-llvm \
		--enable-gallium-egl \
		--enable-gallium-gbm \
		--enable-egl \
		--enable-gles1 \
		--enable-gles2 \
		--enable-openvg \
		--enable-glx-tls \
		--enable-gbm \
		--enable-shared-glapi \
		--enable-shared-dricore \
		--enable-dri \
		--enable-glx \
		--enable-glx-tls \
		--enable-xa \
		--with-x
#		--enable-r600-llvm-compiler

	make
}

package() {
	cd ${srcdir}/$_gitname-build

	make DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
	ln -sf libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so
}
