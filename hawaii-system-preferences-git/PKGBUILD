# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-system-preferences-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii system preferences"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL')
depends=('hawaii-polkit-qt5-git' 'qtconfiguration-git' 'qtaccountsservice-git' 'libhawaii-git')
makedepends=('git' 'cmake' 'qt5-tools' 'qtchooser')
options=('debug')

_gitroot="git://github.com/mauios/hawaii-system-preferences.git"
_gitbranch=dev
_gitname=system-preferences
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export QT_SELECT=5

	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
