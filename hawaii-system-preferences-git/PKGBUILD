# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-system-preferences-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii system preferences"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL')
depends=('kf5-polkit-qt-git' 'qtconfiguration-git' 'qtaccountsservice-git')
makedepends=('git' 'cmake' 'qttools-git')
options=('debug')

_gitroot="git://github.com/mauios/hawaii-system-preferences.git"
_gitbranch=dev
_gitname=system-preferences
source=(${_gitname}::${_gitroot}#branch=${_gitbranch}
        git://github.com/plfiorini/libqtxdg.git)
md5sums=('SKIP' 'SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build

	cd ${srcdir}/${_gitname}
	sed -i -e "s|git://github.com/plfiorini/libqtxdg.git|../libqtxdg|g" \
		.gitmodules
	git submodule init
	git submodule update
}

build() {
	export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
	export QT_SELECT=git

	cd build
	cmake ../${_gitname} \
		-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
		-DPolkitQt-1_DIR=/opt/kf5/lib/cmake/PolkitQt-1 \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=/opt/hawaii-git/lib \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

	# Since we are using Qt from git we create a script that
	# sets LD_LIBRARY_PATH and runs the executable
	path=/opt/hawaii-git
	exe=hawaii-system-preferences
	mv $pkgdir/$path/bin/$exe $pkgdir/$path/bin/$exe.real
	cat > $pkgdir/$path/bin/$exe <<EOF
#!/bin/bash
export LD_LIBRARY_PATH=/opt/qt-git/lib:/opt/kf5/lib:/opt/hawaii-git/lib
exec $path/bin/$exe.real "\$@"
EOF
	chmod 755 $pkgdir/$path/bin/$exe
}
