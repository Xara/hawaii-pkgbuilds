# Maintainer: speps <speps at aur dot archlinux dot org>

_name=qupzilla
pkgname=$_name-qt5-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Cross-platform QtWebKit browser. Qt5 version."
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qtwebkit-git' 'qtscript-git' 'hunspell' 'desktop-file-utils')
makedepends=('qtchooser' 'qttools-git')
optdepends=('bash-completion: bash completion support')
provides=("$_name")
conflicts=("$_name")
install="$_name.install"

_gitroot="git://github.com/QupZilla/qupzilla.git"
_gitbranch=master
_gitname=qupzilla
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_gitname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${srcdir}/${_gitname}

  export PKG_CONFIG_PATH=/opt/qt-git/lib/pkgconfig
  export LD_LIBRARY_PATH=/opt/qt-git/lib
  export QT_SELECT=git

  # enable Wayland support
  export NO_X11=true

  # enable webgl support
  export USE_WEBGL=true

  qmake QUPZILLA_PREFIX=/usr/
  make
}

package() {
  cd ${srcdir}/${_gitname}
  make INSTALL_ROOT="$pkgdir/" install

  # Since we are using Qt from git we create a script that
  # sets LD_LIBRARY_PATH and runs the executable
  mv $pkgdir/usr/bin/qupzilla $pkgdir/usr/bin/qupzilla.real
  cat > $pkgdir/usr/bin/qupzilla <<EOF
#!/bin/bash
export LD_LIBRARY_PATH=/opt/qt-git/lib
exec /usr/bin/qupzilla.real "\$@"
EOF
  chmod 755 $pkgdir/usr/bin/qupzilla
}

# vim:set ts=2 sw=2 et:
