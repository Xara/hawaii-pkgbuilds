# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-shell-rpi-git
pkgver=20150306.0bff65f
pkgrel=1
pkgdesc="Hawaii Shell"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/hawaii-desktop/hawaii-shell"
license=('GPL', 'LGPL')
depends=('qt5-tools' 'qt5-quickcontrols' 'qt5-graphicaleffects' 'qt5-svg' 'qt5-qpa-rpi-plugin-git'
         'greenisland-rpi-git' 'hawaii-wallpapers-git' 'xkeyboard-config'
         'kdoctools' 'kdelibs4support' 'krunner' 'kwayland' 'plasma-framework' 'plasma-workspace' 'plasma-nm'
         'libpulse' 'ttf-dejavu' 'ttf-droid' 'hawaii-icon-themes-git')
optdepends=('weston: nested mode support'
            'pulseaudio: audio support')
makedepends=('git' 'gdb' 'extra-cmake-modules')
conflicts=('hawaii-shell-weston-plugins-git' 'fluid-git' 'libhawaii-git' 'hawaii-shell-git')
replaces=('hawaii-shell-weston-plugins-git' 'fluid-git' 'libhawaii-git' 'hawaii-shell-git')
provides=('hawaii-shell-git')
options=('debug')

_gitroot="git://github.com/hawaii-desktop/hawaii-shell.git"
_gitbranch=master
_gitname=hawaii-shell
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DLIB_INSTALL_DIR=lib \
		-DLIBEXEC_INSTALL_DIR=lib \
		-DQML_INSTALL_DIR=lib/qt/qml \
		-DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
		-DQtWaylandScanner_EXECUTABLE=/usr/lib/qt/bin/qtwaylandscanner \
		-DCUSTOM_QPA_EGLFS=eglfs_rpi \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make VERBOSE=1
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
