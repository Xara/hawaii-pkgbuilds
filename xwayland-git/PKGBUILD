# Maintainer: Yukicanis <yukicanis@gmail.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Zachary Lund <admin@computerquip.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xwayland-git
pkgver=1.14.99.1.217.gfa550cd
pkgrel=1
pkgdesc="Xorg Server with Wayland Support"
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org/"
license=('MIT')
depends=('weston' 'wayland' 'xorg-util-macros' 'fontsproto' 'videoproto'\
         'libxfont' 'glproto' 'xineramaproto')
conflicts=('xorg-server' 'xorg-server-devel' 'xorg-server-common')
makedepends=('git' 'xtrans' 'xf86driproto' 'xcmiscproto' 'bigreqsproto'\
             'resourceproto' 'libxres' 'randrproto' 'fontsproto' 'videoproto'\
             'compositeproto' 'recordproto' 'libxkbfile' 'libxfont' 'mtdev'\
             'scrnsaverproto' 'dri2proto' 'xorg-font-util' 'bison' \
             'dmxproto' 'inputproto' 'libxv')
provides=('xorg-server')
source=('git+git://anongit.freedesktop.org/xorg/xserver#branch=xwayland')
md5sums=('SKIP')

pkgver() {
  cd $srcdir/xserver
  git describe --always | sed 's|xorg-server-||g' | sed 's|-|.|g'
}

build() {
  cd $srcdir/xserver
  ./autogen.sh --prefix=/usr \
      --enable-wayland \
      --enable-ipv6 \
      --enable-dri \
      --disable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make
}

package() {
  cd $srcdir/xserver
  make DESTDIR=${pkgdir} install

  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"
}
