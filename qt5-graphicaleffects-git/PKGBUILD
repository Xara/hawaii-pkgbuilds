# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-graphicaleffects-git
pkgver=0.0.0
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtGraphicalEffects)"
groups=('qt' 'qt5')
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-declarative-git')
makedepends=('gcc' 'git')
replaces=('qt5-qtgraphicaleffects-git')
provides=('qt5-graphicaleffects')
conflicts=('qt5-graphicaleffects')
_gitroot="git://gitorious.org/qt/qtgraphicaleffects.git"
_gitname="qt5-graphicaleffects"
_gitbranch=stable
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}
	qmake
	make
}

package() {
	cd ${srcdir}/${_gitname}
	make INSTALL_ROOT="${pkgdir}" install
}
