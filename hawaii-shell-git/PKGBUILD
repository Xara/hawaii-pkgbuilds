# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-shell-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii Shell"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL', 'LGPL')
depends=('qtchooser' 'qttools-git' 'qtgraphicaleffects-git' 'qtwayland-git' 'qtsvg-git' 'qtscript-git'
         'fluid-git' 'hawaii-wallpapers-git' 'hawaii-widget-styles-git' 'hawaii-icon-themes-git'
         'greenisland-git' 'weston' 'accountsservice' 'alsa-lib'
         'hawaii-polkit-qt5-git' 'qtconfiguration-git' 'qtaccountsservice-git'
         'ttf-dejavu' 'ttf-droid' 'ttf-ubuntu-font-family' 'ttf-raleway-font-family')
makedepends=('git' 'gdb' 'cmake')
conflicts=('hawaii-shell-weston-plugins-git')
replaces=('hawaii-shell-weston-plugins-git')
options=('debug')

_gitroot="git://github.com/mauios/hawaii-shell.git"
_gitbranch=dev
_gitname=hawaii-shell
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export PATH=/opt/qt-git/bin:$PATH
	export PKG_CONFIG_PATH=/opt/hawaii-git/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/hawaii-git/lib:/opt/qt-git/lib
	export QT_SELECT=git

	cd build
	cmake ../${_gitname} \
		-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
		-DPolkitQt-1_DIR=/opt/hawaii-git/lib/cmake/PolkitQt-1 \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=/opt/hawaii-git/lib \
		-DADDITIONAL_LDLIBPATH=/opt/qt-git/lib \
		-DADDITIONAL_PATH=/opt/qt-git/bin \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
