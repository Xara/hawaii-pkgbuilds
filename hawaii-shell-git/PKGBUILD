# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-shell-git
pkgver=20141228.68b7bee
pkgrel=1
pkgdesc="Hawaii Shell"
arch=('i686' 'x86_64')
url="https://github.com/hawaii-desktop/hawaii-shell"
license=('GPL', 'LGPL')
#depends=('qtchooser' 'qt5-tools' 'qt5-graphicaleffects' 'hawaii-qt5-wayland-git' 'qt5-svg' 'qt5-script'
#         'fluid-git' 'hawaii-wallpapers-git' 'hawaii-widget-styles-git' 'hawaii-icon-themes-git'
#         'libhawaii-git' 'weston' 'accountsservice' 'alsa-lib'
#         'hawaii-polkit-qt5-git' 'qtconfiguration-git' 'qtaccountsservice-git'
#         'ttf-dejavu' 'ttf-droid' 'ttf-ubuntu-font-family' 'ttf-raleway-font-family')
depends=('qt5-tools' 'qt5-graphicaleffects' 'hawaii-qt5-wayland-git' 'qt5-svg'
         'greenisland-git' 'weston' 'xkeyboard-config' 'gnome-backgrounds'
         'plasma-framework' 'plasma-workspace' 'plasma-nm'
         'ttf-dejavu' 'hawaii-icon-themes-git')
makedepends=('git' 'gdb' 'cmake' 'qtchooser')
conflicts=('hawaii-shell-weston-plugins-git' 'fluid-git')
replaces=('hawaii-shell-weston-plugins-git' 'fluid-git')
options=('debug')

_gitroot="git://github.com/hawaii-desktop/hawaii-shell.git"
_gitbranch=master
_gitname=hawaii-shell
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
	mkdir -p build
}

build() {
	export QT_SELECT=5

	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make VERBOSE=1
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
