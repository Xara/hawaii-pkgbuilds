# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-shell-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii Shell"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL', 'LGPL')
depends=('qtchooser' 'qt5-tools' 'qt5-graphicaleffects' 'hawaii-qt5-wayland-git' 'qt5-svg' 'qt5-script'
         'fluid-git' 'hawaii-wallpapers-git' 'hawaii-widget-styles-git' 'hawaii-icon-themes-git'
         'libhawaii-git' 'weston' 'accountsservice' 'alsa-lib'
         'hawaii-polkit-qt5-git' 'qtconfiguration-git' 'qtaccountsservice-git'
         'ttf-dejavu' 'ttf-droid' 'ttf-ubuntu-font-family' 'ttf-raleway-font-family')
makedepends=('git' 'gdb' 'cmake')
conflicts=('hawaii-shell-weston-plugins-git')
replaces=('hawaii-shell-weston-plugins-git')
options=('debug')

_gitroot="git://github.com/mauios/hawaii-shell.git"
_gitbranch=dev
_gitname=hawaii-shell
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export QT_SELECT=5

	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo \
		-DQTWAYLAND_SCANNER_EXECUTABLE=/usr/lib/qt/bin/qtwaylandscanner
	make VERBOSE=1
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
