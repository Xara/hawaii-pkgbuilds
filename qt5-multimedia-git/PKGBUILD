# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-multimedia-git
pkgver=0.0.0
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtMultimedia)"
groups=('qt' 'qt5')
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-base-git' 'qt5-xmlpatterns-git' 'qt5-declarative-git' 'qt5-jsbackend-git' 'gstreamer0.10' 'gstreamer0.10-base' 'pulseaudio' 'libpulse')
makedepends=('git')
replaces=('qt5-qtmultimedia-git')
provides=('qt5-multimedia')
conflicts=('qt5-multimedia')
options=('!libtool')
_gitroot="git://gitorious.org/qt/qtmultimedia.git"
_gitname="qt5-multimedia"
_gitbranch=stable
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}
	qmake
	make
}

package() {
	cd "${_gitname}"
	make INSTALL_ROOT="${pkgdir}" install
}
