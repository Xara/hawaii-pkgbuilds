# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

# Based on qt5-qtbase-git from Sergio Correia <sergio.correia@uece.net>

pkgname=qt5-base-git
pkgver=20130524
pkgrel=1
pkgdesc="A cross-platform application and UI framework"
groups=('qt' 'qt5')
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('pcre' 'libpng' 'libjpeg' 'dbus-core' 'cups' 'sqlite' 'libxcb'
         'xcb-proto' 'xcb-util' 'xcb-util-image' 'xcb-util-keysyms'
         'xcb-util-renderutil' 'xcb-util-wm' 'libxext' 'libxrender'
         'icu' 'systemd-tools' 'mesa' 'qtchooser')
replaces=('qt5-qtbase-for-hawaii-git' 'qt5-qtbase-git')
conflicts=('qt5-base')
provides=('qt5-base')
source=()
md5sums=()
makedepends=('git' 'gcc')

_gitroot="git://gitorious.org/qt/qtbase.git"
_gitname=qt5-qtbase
_gitbranch=stable

build() {
	if [ ! -d "${_gitname}" ]; then
		git clone -b ${_gitbranch} --depth 1 ${_gitroot} ${_gitname} && cd ${_gitname}
	else
		cd ${_gitname} && git reset --hard && git pull origin && git clean -dfx
	fi

	msg "GIT checkout done."

	sed -i "s|-O2|${CXXFLAGS}|" mkspecs/common/{g++,gcc}-base.conf
	sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
	sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS}|g" mkspecs/common/gcc-base.conf

	./configure -confirm-license -opensource \
		-prefix /usr \
		-bindir /usr/lib/qt/bin \
		-docdir /usr/share/doc/qt \
		-headerdir /usr/include/qt \
		-archdatadir /usr/lib/qt \
		-datadir /usr/share/qt \
		-sysconfdir /etc/xdg \
		-examplesdir /usr/share/doc/qt/examples \
		-system-sqlite \
		-openssl-linked \
		-nomake docs \
		-nomake examples \
		-nomake tests \
		-no-rpath \
		-optimized-qmake \
		-dbus-linked \
		-reduce-relocations \
		-opengl es2
	make
}

package() {
	cd "${_gitname}"

	make INSTALL_ROOT="${pkgdir}" install


	# Useful symlinks
	install -d "${pkgdir}"/usr/bin
	for b in "${pkgdir}"/usr/lib/qt/bin/*; do
		ln -s /usr/lib/qt/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
	done
}
