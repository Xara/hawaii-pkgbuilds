# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=eyesight
pkgver=0.1.2
pkgrel=1
pkgdesc="EyeSight: An image viewing program"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL2')
depends=('qt5-base' 'qt5-tools')
optdepends=('qt5-svg: for svg support'
            'qt5-imageformats: for mng, tga, tiff, bmp image support')
makedepends=('cmake')
conflicts=('hawaii-eyesight')
replaces=('hawaii-eyesight')
options=('debug')
source=("http://sourceforge.net/projects/mauios/files/hawaii/${pkgname}/hawaii-${pkgname}-${pkgver}.tar.gz")
md5sums=("bab49955b5a412f65961bee762e8818e")

prepare() {
	mkdir -p build
}

build() {
	export QT_SELECT=git

	cd build
	cmake ../hawaii-${pkgname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
