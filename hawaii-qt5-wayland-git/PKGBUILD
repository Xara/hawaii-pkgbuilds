# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-qt5-wayland-git
pkgver=v5.4.0.alpha1.20.gc79ab2b
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtWayland)"
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-base' 'qt5-declarative' 'libxcomposite' 'libxrender' 'wayland')
makedepends=('git' 'gdb' 'qtchooser')
options=('!libtool' 'debug')
#source=(git://gitorious.org/qt/qtwayland.git#branch=dev)
source=(git://github.com/maui-packages/qtwayland#branch=upstream54)
md5sums=('SKIP')
_ver=5.5.0

pkgver() {
  cd qtwayland
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd qtwayland
}

build() {
  export QT_SELECT=5

  cd qtwayland
  qmake CONFIG+=wayland-compositor QMAKE_LIBS_PRIVATE+="-lfontconfig -lfreetype -lXrender"
  make
}

package() {
  cd qtwayland
  make INSTALL_ROOT="${pkgdir}" install

  # Workaround to install generated private headers
  cp ./include/QtCompositor/${_ver}/QtCompositor/private/{qwayland-server-*,*protocol*}.h \
      ${pkgdir}/usr/include/qt/QtCompositor/${_ver}/QtCompositor/private/
}
