# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=kde-kconfig-git
pkgver=0.1.0
pkgrel=1
pkgdesc="KConfig from KF5"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('kde-kcoreaddons-git')
makedepends=('cmake' 'extra-cmake-modules-git' 'git' 'qtchooser')
options=('debug')
source=('git://github.com/hawaii-desktop/kconfig')
md5sums=('SKIP')

pkgver() {
  cd kconfig
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  export PKG_CONFIG_PATH=/opt/qt-git/lib/pkgconfig
  export LD_LIBRARY_PATH=/opt/qt-git/lib
  export QT_SELECT=git

  cd build
  cmake ../kconfig \
    -DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
