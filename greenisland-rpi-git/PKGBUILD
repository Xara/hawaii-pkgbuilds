# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

buildarch=20

pkgname=greenisland-rpi-git
pkgver=20150303.045845b
pkgrel=1
pkgdesc="Green Island: Qt-based compositor infrastructure for Wayland"
arch=('armv6h' 'armv7h')
url='https://github.com/greenisland/greenisland.git'
license=('GPL', 'LGPL')
depends=('qt5-declarative' 'hawaii-qt5-wayland-rpi-git' 'libkscreen-frameworks' 'xkeyboard-config')
optdepends=('weston-rpi: nested mode support')
provides=('greenisland-git' 'greenisland')
conflicts=('greenisland-git' 'greenisland')
replaces=('greenisland-git' 'greenisland')
makedepends=('git' 'gdb' 'extra-cmake-modules')
options=('debug')

_gitroot="git://github.com/greenisland/greenisland.git"
_gitbranch=master
_gitname=greenisland
source=(${_gitname}::${_gitroot}#branch=${_gitbranch}
        '0001-Link-to-Broadcom-libraries.patch'
        'bcm_host.pc'
        'egl.pc'
        'glesv2.pc')
md5sums=('SKIP'
         '4f6eff004fe11d204fbe61a95b43aae6'
         'e8a462fdb2ce97713e81cf00a83a7a68'
         'afea4d0efb86116244b06b1804ea09c0'
         'c4cdceb2dbc844213f9440f91f55c320')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
	cd ${srcdir}/${_gitname}
	patch -p1 < $srcdir/0001-Link-to-Broadcom-libraries.patch
	cd -

	mkdir build
}

build() {
	cd build

	export PKG_CONFIG_LIBDIR=$srcdir

	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DLIB_INSTALL_DIR=lib \
		-DLIBEXEC_INSTALL_DIR=lib \
		-DQML_INSTALL_DIR=lib/qt/qml \
		-DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
		-DQtWaylandScanner_EXECUTABLE=/usr/lib/qt/bin/qtwaylandscanner \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
