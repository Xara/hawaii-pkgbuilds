# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

# Based on xorg-server from Jan de Groot <jgc@archlinux.org>

pkgbase=xorg-server-wayland-git
pkgname=('xorg-server-wayland-git' 'xorg-server-xephyr-wayland-git' 'xorg-server-xdmx-wayland-git' 'xorg-server-xvfb-wayland-git' 'xorg-server-xnest-wayland-git' 'xorg-server-common-wayland-git' 'xorg-server-devel-wayland-git')
pkgver=20121209
pkgrel=2
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('git' 'pixman' 'libx11' 'mesa-full-wayland' 'libgl' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto' 'randrproto' 'inputproto'
             'fontsproto' 'videoproto' 'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont' 'renderproto' 'libpciaccess'
             'libxv' 'xf86dgaproto' 'libxmu' 'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst' 'libxres' 'xorg-xkbcomp' 'xorg-util-macros'
             'xorg-font-util' 'glproto' 'dri2proto' 'libgcrypt')
options=('!libtool')
source=(autoconfig-nvidia.patch
        autoconfig-sis.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf
        git-fixes.patch
        use-pixman-glyph-cache.patch)
sha256sums=('66e25f76a7496c429e0aff4b0670f168719bb0ceaeb88c6f2272f2bf3ed21162'
            'd027776fac1f7675b0a9ee817502290b1c45f9c09b0f0a6bb058c35f92361e84'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166'
            'd49e534b77ead809eb873a1a86cd47c791a8ddd10557bfa635197bba6bc00b71'
            '7c1dacd30c46b01424c7c75974285cd348dafd0327eb3f211cbe446ad90dd308')

_gitroot="git://git.collabora.com/git/user/daniels/xserver"
_gitname="xserver"
_githash="xwayland-1.13"

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $srcdir/$_gitname ] ; then
    cd $_gitname && git pull origin master
      msg "The local files are updated."
  else
      git clone $_gitroot
  fi

  cd $_gitname && git checkout $_githash || exit $?

  msg "Creating build directory..."
  rm -rf ${srcdir}/$_gitname-build
  cp -r ${srcdir}/$_gitname ${srcdir}/$_gitname-build

  msg "Starting build..."
  cd ${srcdir}/$_gitname-build

  # Apply upstream fixes from server-1.13 branch
#  patch -Np1 -i "${srcdir}/git-fixes.patch"

  # Use pixman 0.28.0 glyph cache - backported from git master
#  patch -Np1 -i "${srcdir}/use-pixman-glyph-cache.patch"

  # Use nouveau/nv/nvidia drivers for nvidia devices
#  patch -Np1 -i "${srcdir}/autoconfig-nvidia.patch"

  # Use unofficial imedia SiS driver for supported SiS devices
#  patch -Np0 -i "${srcdir}/autoconfig-sis.patch"

  ./autogen.sh --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-wayland-git() {
  pkgdesc="Xorg server common files"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap' 'xorg-fonts-misc')
  provides=("xorg-server-common=1.13.0")
  conflicts=('xorg-server-common')

  cd ${srcdir}/$_gitname-build
  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-common-wayland-git"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xorg-server-common-wayland-git"
  
  make -C xkb DESTDIR="${pkgdir}" install-data

  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/Xserver.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"
}

package_xorg-server-wayland-git() {
  pkgdesc="Xorg X server"
  depends=(libxdmcp libxfont libpciaccess libdrm wayland-git 'pixman>=0.28.0' libgcrypt libxau xorg-server-common-wayland-git xf86-input-evdev)
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  # see xorg-server-1.13.0/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
  provides=('X-ABI-VIDEODRV_VERSION=13' 'X-ABI-XINPUT_VERSION=18' 'X-ABI-EXTENSION_VERSION=7.0' 'x-server' "xorg-server=1.13.0")
  conflicts=('nvidia-utils<=290.10' 'xorg-server')
  groups=('xorg')

  cd ${srcdir}/$_gitname-build
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "${pkgdir}/var"

  rm -f "${pkgdir}/usr/share/man/man1/Xserver.1"
  rm -f "${pkgdir}/usr/lib/xorg/protocol.txt"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-wayland-git"
  ln -sf ../xorg-server-common-wayland-git/COPYING "${pkgdir}/usr/share/licenses/xorg-server-wayland-git/COPYING"

  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/share/aclocal"
}

package_xorg-server-xephyr-wayland-git() {
  pkgdesc="A nested X server that runs as an X application"
  depends=(libxfont libgl libgcrypt libxv 'pixman>=0.28.0' xorg-server-common-wayland-git)
  provides=("xorg-server-xephyr=1.13.0")
  conflicts=('xorg-server-xephyr')

  cd ${srcdir}/$_gitname-build
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xephyr-wayland-git"
  ln -sf ../xorg-server-common-wayland-git/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xephyr-wayland-git/COPYING"
}

package_xorg-server-xvfb-wayland-git() {
  pkgdesc="Virtual framebuffer X server"
  depends=(libxfont libxdmcp libxau libgcrypt 'pixman>=0.28.0' xorg-server-common-wayland-git)
  provides=("xorg-server-xvfb=1.13.0")
  conflicts=('xorg-server-xvfb')

  cd ${srcdir}/$_gitname-build
  make DESTDIR="${pkgdir}" install

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xvfb-wayland-git"
  ln -sf ../xorg-server-common-wayland-git/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xvfb-wayland-git/COPYING"
}

package_xorg-server-xnest-wayland-git() {
  pkgdesc="A nested X server that runs as an X application"
  depends=(libxfont libxext libgcrypt 'pixman>=0.28.0' xorg-server-common-wayland-git)
  provides=("xorg-server-xnest=1.13.0")
  conflicts=('xorg-server-xnest')

  cd ${srcdir}/$_gitname-build
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xnest-wayland-git"
  ln -sf ../xorg-server-common-wayland-git/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xnest-wayland-git/COPYING"
}

package_xorg-server-xdmx-wayland-git() {
  pkgdesc="Distributed Multihead X Server and utilities"
  depends=(libxfont libxi libgcrypt libxaw libxrender libdmx libxfixes 'pixman>=0.28.0' xorg-server-common-wayland-git)
  provides=("xorg-server-xdmx=1.13.0")
  conflicts=('xorg-server-xdmx')

  cd ${srcdir}/$_gitname-build
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xdmx-wayland-git"
  ln -sf ../xorg-server-common-wayland-git/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xdmx-wayland-git/COPYING"
}

package_xorg-server-devel-wayland-git() {
  pkgdesc="Development files for the X.Org X server"
  depends=(# see pkgdir/usr/lib/pkgconfig/xorg-server.pc
           xproto randrproto renderproto xextproto inputproto kbproto 
           fontsproto pixman videoproto xf86driproto glproto 
           mesa-full-wayland dri2proto xineramaproto libpciaccess
           # see FS#32146
           resourceproto scrnsaverproto
           # not technically required but almost every Xorg pkg needs it to build
           xorg-util-macros)
  provides=("xorg-server-devel=1.13.0")
  conflicts=('xorg-server-devel')

  cd ${srcdir}/$_gitname-build
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/share/man"
  rm -rf "${pkgdir}/usr/share/doc"
  rm -rf "${pkgdir}/usr/share/X11"
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-devel-wayland-git"
  ln -sf ../xorg-server-common-wayland-git/COPYING "${pkgdir}/usr/share/licenses/xorg-server-devel-wayland-git/COPYING"
}
