# Maintainer: RKA KriK <rka_krik@mail.ru>

pkgname=gtk3-git
pkgver=20110824
pkgrel=2
_realver=3.1.12
pkgdesc="The GTK+ Toolkit (v3.1.x, unstable, with wayland support)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('glib2-git' 'atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 
		'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 
		'shared-mime-info' 'libxkbcommon-git' 'pango-unstable')
makedepends=('gobject-introspection')
conflicts=('gtk3')
provides=("gtk3=${_realver}")
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(settings.ini)
sha256sums=('3f2fd1ac4e0a59381e443b7c14d937b434c515e02d06c7c4b3999fb24406b784')

_gitroot="git://git.gnome.org/gtk+"
_gitname="gtk+"

build() {
  msg "Connecting to gnome.org GIT server...."

  if [ -d ${srcdir}/${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Creating build directory"
  if [ -d ${srcdir}/${_gitname}-build ]; then rm -rf ${srcdir}/${_gitname}-build; fi
  cp -R ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build

  msg "Starting make..."

  cd ${srcdir}/${_gitname}-build
  CXX=/bin/false ./autogen.sh --prefix=/usr --sysconfdir=/etc \
  		--localstatedir=/var --enable-gtk2-dependency \
  		--enable-x11-backend --enable-wayland-backend \
  		--disable-schemas-compile #--disable-glibtest

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  #sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
