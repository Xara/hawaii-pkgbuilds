# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=eyesight-git
pkgver=0.0.0
pkgrel=1
pkgdesc="EyeSight: An image viewing program"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL2')
depends=('qt5-base' 'qt5-tools')
optdepends=('qt5-svg: for svg support'
            'qt5-imageformats: for mng, tga, tiff, bmp image support')
makedepends=('git' 'cmake')
conflicts=('hawaii-eyesight-git')
replaces=('hawaii-eyesight-git')
options=('debug')

_gitroot="git://github.com/mauios/eyesight.git"
_gitbranch=dev
_gitname=eyesight
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir build
}

build() {
	export QT_SELECT=5

	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
