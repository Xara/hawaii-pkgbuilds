pkgname=sddm-qt5-git
pkgver=412.4f2c54b
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=('i686' 'x86_64')
url="http://github.com/sddm/sddm"
license=('GPL')
depends=('pam' 'qtbase-git' 'qtdeclarative-git' 'libx11')
makedepends=('cmake' 'git' 'qtchooser')
provides=('sddm')
replaces=('sddm')

_gitroot="git://github.com/sddm/sddm.git"
_gitbranch=master
_gitname=sddm
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_gitname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
  export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
  export QT5_SELECT=git

  cd ${srcdir}/${_gitname}

  mkdir -p build
  cd build

  cmake .. \
	-DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT5=true \
	-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake
  make
}

package() {
  cd ${srcdir}/${_gitname}/build
  make DESTDIR="${pkgdir}" install

  # Since we are using Qt from git we create a script that
  # sets LD_LIBRARY_PATH and runs the executable
  path=/usr
  exe=sddm-greeter
  mv $pkgdir/$path/bin/$exe $pkgdir/$path/bin/$exe.real
  cat > $pkgdir/$path/bin/$exe <<EOF
#!/bin/bash
export LD_LIBRARY_PATH=/opt/qt-git/lib
$path/bin/$exe.real $@
EOF
  chmod 755 $pkgdir/$path/bin/$exe
}
