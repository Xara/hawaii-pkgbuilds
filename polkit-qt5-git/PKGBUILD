# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=polkit-qt5-git
pkgver=0.0.0
pkgrel=1
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/polkit-qt-1'
license=('LGPL')
depends=('polkit' 'qt5-base-git')
replaces=('polkit-for-hawaii-git')
makedepends=('git' 'cmake' 'extra-cmake-modules-git' 'qt5-svg-git')
#_gitroot="git://anongit.kde.org/polkit-qt-1"
_gitroot="git://github.com/hawaii-desktop/polkit-qt-1.git"
_gitname="qt5-polkit"
_gitbranch="hawaii"
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}

	mkdir build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/opt/hawaiide \
		-DLIB_INSTALL_DIR=lib \
		-DCMAKE_BUILD_TYPE=Release ..
	make
}

package() {
	cd ${srcdir}/${_gitname}/build
	make DESTDIR="${pkgdir}" install
}
