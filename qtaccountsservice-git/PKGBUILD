# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qtaccountsservice-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Qt-style API for AccountsService"
arch=('i686' 'x86_64')
url="https://github.com/mauios/qtaccountsservice"
license=('LGPL')
depends=('qt5-base' 'qt5-declarative')
makedepends=('git' 'cmake' 'qtchooser')
conflicts=('qt-accountsservice-addon-git')
replaces=('qt-accountsservice-addon-git')
_gitroot="git://github.com/mauios/qtaccountsservice.git"
_gitname="qtaccountsservice"
_gitbranch="dev"
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export QT_SELECT=5

	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
